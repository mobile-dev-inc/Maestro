plugins {
    id "kotlin"
    id 'application'
    id 'org.jreleaser' version '1.0.0'
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

application {
    applicationName = 'maestro'
    mainClassName = 'maestro.cli.AppKt'
}

jar {
    manifest {
        attributes 'Main-Class': 'maestro.cli.AppKt'
    }
}

run {
    standardInput = System.in
    workingDir = rootDir
}

dependencies {
    implementation project(path: ':maestro-utils')
    annotationProcessor 'info.picocli:picocli-codegen:4.5.2'

    implementation project(':maestro-client')
    implementation project(':maestro-orchestra')
    implementation project(':maestro-network-proxy')
    implementation project(':maestro-ios')
    implementation project(':maestro-studio:server')
    implementation "dev.mobile:dadb:1.2.6"
    implementation 'info.picocli:picocli:4.5.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.4'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.4'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.4'
    implementation 'org.fusesource.jansi:jansi:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'io.ktor:ktor-client-core:2.1.2'
    implementation 'io.ktor:ktor-client-cio:2.1.2'
    implementation 'org.rauschig:jarchivelib:1.2.0'

    // Silence logs from WireMock
    implementation 'org.slf4j:slf4j-nop:2.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation "com.google.truth:truth:1.1.3"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = CLI_VERSION
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

distZip {
    archiveName 'maestro.zip'
}
distTar {
    archiveName 'maestro.tar'
}

jreleaser {
    version = CLI_VERSION
    gitRootSearch = true

    project {
        website = 'https://maestro.mobile.dev'
        description = 'Maestro CLI'
        authors = ['Dmitry Zaytsev', 'Amanjeet Singh', 'Leland Takamine', 'Arthur Saveliev', 'Axel Niklasson', 'Berik Visschers']
        license = 'Apache-2.0'
    }

    release {
        github {
            owner = 'mobile-dev-inc'
            name = 'maestro'
            tagName = "cli-$CLI_VERSION"
            releaseName = "CLI $CLI_VERSION"
            overwrite = true
        }
    }
    distributions {
        maestro {
            artifact {
                path = 'build/distributions/maestro.zip'
            }
            brew {
                extraProperties.put('skipJava', 'true')
                active = 'RELEASE'
                formulaName = 'Maestro'

                repoTap {
                    owner = 'mobile-dev-inc'
                    name = 'homebrew-tap'
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}
