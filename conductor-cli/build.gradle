plugins {
    id "kotlin"
    id 'application'
    id 'org.jreleaser' version '1.0.0'
}

application {
    applicationName = 'conductor'
    mainClassName = 'conductor.cli.AppKt'
}

run {
    standardInput = System.in
    workingDir = rootDir
}

dependencies {
    annotationProcessor 'info.picocli:picocli-codegen:4.5.2'

    implementation project(':conductor-client')
    implementation project(':conductor-orchestra')
    implementation "dev.mobile:dadb:0.0.9"
    implementation 'info.picocli:picocli:4.5.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.1'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jreleaser {
    version = CLI_VERSION
    gitRootSearch = true

    project {
        website = 'https://conductor.mobile.dev'
        description = 'Conductor CLI'
        authors = ['Dmitry Zaytsev']
        license = 'Apache-2.0'
    }

    release {
        github {
            owner = 'mobile-dev-inc'
            name = 'conductor'
            tagName = "cli-$CLI_VERSION"
            releaseName = "CLI $CLI_VERSION"
            overwrite = true
        }
    }
    distributions {
        conductor {
            artifact {
                path = 'build/distributions/conductor-{{projectVersion}}.zip'
            }
            brew {
                active = 'RELEASE'
                formulaName = 'Conductor'

                repoTap {
                    owner = 'mobile-dev-inc'
                    name = 'homebrew-tap'
                }

                dependencies {
                    dependency('openjdk@11')
                }

                extraProperties.put("skipJava", true)
            }
        }
    }
}
