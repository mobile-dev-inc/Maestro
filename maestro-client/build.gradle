plugins {
    id 'maven-publish'
    id "kotlin"
    id "com.google.protobuf" version "0.8.18"
    id "idea"
    id "com.vanniktech.maven.publish"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.18.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.40.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
            task.builtins {
                kotlin {}
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += 'build/generated/source/proto/main/grpc'
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/kotlin'
        }
    }
}

dependencies {
    protobuf project(':maestro-proto')

    api 'io.grpc:grpc-kotlin-stub:1.2.1'
    api 'io.grpc:grpc-stub:1.45.0'
    api 'io.grpc:grpc-netty:1.40.1'
    api 'io.grpc:grpc-protobuf:1.45.0'
    api 'io.grpc:grpc-okhttp:1.45.0'
    api 'com.google.protobuf:protobuf-kotlin:3.19.4'
    api 'com.michael-bull.kotlin-result:kotlin-result:1.1.14'
    api "dev.mobile:dadb:1.1.0"
    api "org.slf4j:slf4j-simple:1.7.36"
    api 'com.squareup.okio:okio:3.2.0'
    api 'com.github.romankh3:image-comparison:4.4.0'
    api "com.android.tools:sdk-common:30.3.0"
    api "com.android.tools.apkparser:apkanalyzer:30.3.0"

    implementation project(':maestro-ios')
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'de.upb.cs.swt:axml:2.1.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation "com.google.truth:truth:1.1.3"
}

plugins.withId("com.vanniktech.maven.publish") {
    mavenPublish {
        sonatypeHost = "S01"
    }
}

test {
    useJUnitPlatform()
}
