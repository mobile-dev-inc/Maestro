import com.vanniktech.maven.publish.SonatypeHost
import org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompilerOptions
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    id("maven-publish")
    id("application")
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.mavenPublish)
}

application {
    applicationName = "maestro-studio"
    mainClass = "maestro.studio.ServerKt"
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.S01)
}

def copyWebFiles = tasks.register("copyWebFiles", Copy.class) {
    dependsOn(":maestro-studio:web:build")
    from(new File(project(":maestro-studio:web").projectDir, "build"))
    into(new File(projectDir, "src/main/resources/web"))
}

tasks.compileJava {
    dependsOn(copyWebFiles)
}

tasks.processResources {
    dependsOn(copyWebFiles)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(8)
}

tasks.withType(KotlinCompilationTask<KotlinJvmCompilerOptions>).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_1_8)
        freeCompilerArgs.addAll("-Xjdk-release=1.8")
    }
}

dependencies {
    implementation(project(":maestro-orchestra"))
    implementation(project(":maestro-client"))
    implementation(project(":maestro-utils"))
    implementation(libs.ktor.server.core)
    implementation(libs.ktor.server.netty)
    implementation(libs.ktor.server.cors)
    implementation(libs.ktor.server.status.pages)
    implementation(libs.jackson.module.kotlin)
    implementation(libs.square.okhttp)
}
