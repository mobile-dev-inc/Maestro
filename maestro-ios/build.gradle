plugins {
    id 'maven-publish'
    id "kotlin"
    id "com.google.protobuf" version "0.9.1"
    id "com.vanniktech.maven.publish"
    id "idea"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.9"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.50.2'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
            task.builtins {
                kotlin {}
            }
        }
    }
}

compileKotlin {
    dependsOn generateProto
}

kotlin.sourceSets.all {
    // Prevent build warnings for grpc's generated opt-in code
    languageSettings.optIn("kotlin.RequiresOptIn")
}

sourceSets {
    main {
        java {
            srcDirs += 'build/generated/source/proto/main/grpc'
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/kotlin'
        }
    }
}

dependencies {
    implementation project(':maestro-utils')
    implementation project(':maestro-xcuitest-driver')

    implementation(libs.grpc.kotlin.stub)
    implementation(libs.grpc.stub)
    implementation(libs.grpc.protobuf)
    implementation(libs.grpc.okhttp)
    implementation(libs.google.protobuf.kotlin)
    implementation(libs.kotlin.result)
    implementation(libs.slf4j)
    implementation(libs.square.okio)
    api(libs.google.gson)
    api(libs.square.okhttp)
    api(libs.appdirs)
    api(libs.jackson.module.kotlin)

    testImplementation(libs.junit.jupiter.api)
    testRuntimeOnly(libs.junit.jupiter.engine)
    testImplementation(libs.google.truth)
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

plugins.withId("com.vanniktech.maven.publish") {
    mavenPublish {
        sonatypeHost = "S01"
    }
}

test {
    useJUnitPlatform()
}
